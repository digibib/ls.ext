PREFIX deich: <__ONTOLOGY__>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  role: <http://data.deichman.no/role#>

CONSTRUCT {
       ?work a deich:Work ;
               deich:mainTitle ?mainTitle ;
               deich:partTitle ?partTitle ;
               deich:publicationYear ?publicationYear ;
               deich:publication ?pub ;
               deich:contributor ?workContrib ;
               deich:subject ?subject .
        ?pub a deich:Publication ;
               deich:mainTitle ?pubMainTitle ;
               deich:partTitle ?pubPartTitle ;
               deich:language ?language ;
               deich:format ?format ;
               deich:issued ?pubPublicationYear ;
               deich:audience ?audience ;
               deich:contributor ?pubContrib .
?workContrib a deich:Contribution ;
               deich:role ?role ;
               deich:agent ?agent .
 ?pubContrib a deich:Contribution ;
               deich:role ?pubRole ;
               deich:agent ?pubAgent .
   ?pubAgent a deich:Agent ;
               deich:name ?pubAgentName ;
               deich:birthYear ?pubAgentBirthYear ;
               deich:deathYear ?pubAgentDeathYear .
      ?agent a deich:Agent ;
               deich:name ?name ;
               deich:birthYear ?birthYear ;
               deich:deathYear ?deathYear .
      ?subject deich:name ?subjectName .
}
WHERE {
  BIND(uri("__WORKURI__") AS ?work)
  ?work a deich:Work .

  OPTIONAL { ?work deich:mainTitle ?mainTitleO . BIND(str(?mainTitleO) as ?mainTitle) }
  OPTIONAL { ?work deich:partTitle ?partTitleO . BIND(str(?partTitleO) as ?partTitle) }
  OPTIONAL { ?work deich:publicationYear ?publicationYearO . BIND(str(?publicationYearO) AS ?publicationYear) }
  OPTIONAL { ?work deich:audience ?audienceO . BIND(str(?audienceO) AS ?audience) }

  OPTIONAL {
    ?work deich:subject ?subject .
    ?subject a deich:Subject ;
            deich:name ?subjectName  .
  }

  OPTIONAL {
    ?work deich:contributor ?workContrib .
    ?workContrib a deich:Contribution ;
             deich:agent ?agent ;
             deich:role ?roleURI .
    ?agent deich:name ?name .
    BIND(str(?roleURI) AS ?role)
  }
  OPTIONAL { ?agent deich:birthYear ?birthYearO . BIND(str(?birthYearO) AS ?birthYear) }
  OPTIONAL { ?agent deich:deathYear ?deathYearO . BIND(str(?deathYearO) AS ?deathYear) }

  OPTIONAL {
    ?pub a deich:Publication ;
         deich:publicationOf ?work .
    OPTIONAL { ?pub deich:mainTitle ?pubMainTitle }
    OPTIONAL { ?pub deich:partTitle ?pubPartTitle }
    OPTIONAL { ?pub deich:contributor ?pubContrib .
               ?pubContrib a deich:Contribution ;
                            deich:agent ?pubAgent ;
                            deich:role ?pubRoleURI .
               ?pubAgent deich:name ?pubAgentName .
               BIND(str(?pubRoleURI) AS ?pubRole)
    }
  }
  OPTIONAL { ?pubAgent deich:birthYear ?pBirthYearO . BIND(str(?pBirthYearO) AS ?pubAgentBirthYear) }
  OPTIONAL { ?pubAgent deich:deathYear ?pDeathYearO . BIND(str(?pDeathYearO) AS ?pubAgentDeathYear) }

  OPTIONAL { ?pub deich:language ?languageO . BIND(str(?languageO) AS ?language) }
  OPTIONAL { ?pub deich:format ?formatO . BIND(str(?formatO) AS ?format) }
  OPTIONAL { ?pub deich:publicationYear ?pubPublicationYearO . BIND(str(?pubPublicationYearO) AS ?pubPublicationYear) }

}

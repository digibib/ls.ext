PREFIX deich: <__ONTOLOGY__>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  role: <http://data.deichman.no/role#>
PREFIX   afn: <http://jena.apache.org/ARQ/function#>

CONSTRUCT {
        <__PUBLICATIONURI__> a deich:Publication ;
               deich:mainTitle ?pubMainTitle ;
               deich:partTitle ?pubPartTitle ;
               deich:norwegianTitle ?norwegianTitle ;
               deich:englishTitle ?englishTitle ;
               deich:languages ?language ;
               deich:language ?langCode ;
               deich:format ?format ;
               deich:publicationYear ?pubPublicationYear ;
               deich:firstPublicationYear ?workPublicationYear ;
               deich:audience ?audience ;
               deich:hasHoldingBranch ?branch ;
               deich:contributor ?contrib ;
               deich:hasImage ?img ;
               deich:mediaType ?pubMediaType ;
               deich:mt ?mtNo ;
               deich:hasMediaTypeAll ?mediaType ;
               deich:workUri ?workUri ;
               deich:subject ?subjectLabel ;
               deich:genre ?genreLabel ;
               deich:audience ?audience ;
               deich:agents ?contribAgentName ;
               deich:agents ?pubPartAgentName ;
               ?contribRoleURI ?contribAgentName ;
               ?pubPartRoleURI ?pubPartAgentName ;
               deich:mainEntryName ?mainEntryName ;
               deich:recordId ?recordId ;
               deich:summary ?summary ;
               deich:formatNo ?formatNo ;
               deich:formatAdaptation ?formatAdaptNo ;
               deich:ageLimit ?ageLimit ;
               deich:series ?seriesTitle ;
               deich:dewey ?dewey ;
               deich:nationality ?nationLabel ;
               deich:title ?pubMainTitle ;
               deich:title ?pubPartTitle ;
               deich:title ?pubSubtitle ;
               deich:title ?pubPartNumber ;
               deich:title ?pubPartMainTitle ;
               deich:litform ?litform ;
               deich:subtitles ?subtitles ;
               deich:subtitle ?pubSubtitle ;
               deich:partNumber ?pubPartNumber ;
               deich:isbn ?isbn ;
               deich:ismn ?ismn ;
               deich:ean ?ean ;
               deich:publishedBy ?publishedBy ;
               deich:compType ?compTypeLabel ;
               deich:fictionNonfiction ?fictionNonfiction ;
               deich:langTitles <http://ignore> ;
               deich:kd ?locDewey .
 <http://ignore> ?anyLang [
                deich:mainTitle ?anyMt;
                deich:subtitle ?anySt ;
                deich:partNumber ?anyPn ;
                deich:partTitle ?anyPt ;
                deich:hasImage ?anyImage ;
                deich:pubUri ?anyPub ]  .
  ?mediaType a deich:Mediatype ;
               deich:format ?mtFormat .
    ?contrib a deich:Contribution ;
               deich:role ?contribRole ;
               deich:agent ?contribAgent ;
               deich:mainEntry ?mainEntry .
?contribAgent a deich:Agent ;
               deich:name ?contribAgentName ;
               deich:birthYear ?contribAgentBirthYear ;
               deich:deathYear ?contribAgentDeathYear .
}
WHERE {
  # 1. Information from publication:

        { <__PUBLICATIONURI__> deich:recordId ?recordId ;
            deich:publicationOf ?work  .
            BIND(str(?work) AS ?workUri ) }
  UNION { <__PUBLICATIONURI__> deich:fictionNonfiction ?ficturi .BIND(str(?ficturi) AS ?fictionNonfiction) }
  UNION { <__PUBLICATIONURI__> deich:hasMediaType ?mturi .
          ?mturi rdfs:label ?mtNoLang .
          BIND(str(?mtNoLang) AS ?mtNo)
          FILTER(lang(?mtNoLang) = "no")
          BIND(str(?mturi) AS ?pubMediaType) }
  UNION { <__PUBLICATIONURI__> deich:mainTitle ?pubMainTitle }
  UNION { <__PUBLICATIONURI__> deich:partTitle ?pubPartTitle }
  UNION { <__PUBLICATIONURI__> deich:subtitle ?pubSubtitle }
  UNION { <__PUBLICATIONURI__> deich:partNumber ?pubPartNumber }
  UNION { <__PUBLICATIONURI__> deich:hasHoldingBranch ?branch }
  UNION { <__PUBLICATIONURI__> deich:isbn ?isbn }
  UNION { <__PUBLICATIONURI__> deich:ismn ?ismn }
  UNION { <__PUBLICATIONURI__> deich:ean ?ean }
  UNION { <__PUBLICATIONURI__> deich:language ?languageO .
          BIND(str(?languageO) AS ?language) .
          BIND(afn:substr(?language, 29) AS ?langCode) }
  UNION { <__PUBLICATIONURI__> deich:hasSubtitles ?subLang .
            BIND(str(?subLang) AS ?subLangStr) .
            BIND(afn:substr(?subLangStr, 29) AS ?subtitles) }
  UNION { <__PUBLICATIONURI__> deich:format ?formatO . BIND(str(?formatO) AS ?format) }
  UNION { <__PUBLICATIONURI__> deich:format ?formatURI .
          ?formatURI rdfs:label ?formatNoLang .
          BIND(str(?formatNoLang) AS ?formatNo)
          FILTER(lang(?formatNoLang) = "no") }
  UNION { <__PUBLICATIONURI__> deich:publicationYear ?pubPublicationYearO . BIND(str(?pubPublicationYearO) AS ?pubPublicationYear) }
  UNION { <__PUBLICATIONURI__> deich:hasImage ?img }
  UNION { <__PUBLICATIONURI__> deich:hasSummary ?summary }
  UNION { <__PUBLICATIONURI__> deich:ageLimit ?maybeAgeLimit . BIND(IF(BOUND(?maybeAgeLimit), ?maybeAgeLimit, "0") AS ?ageLimit) }

  UNION { <__PUBLICATIONURI__> deich:inSerial ?serialBnode .
          ?serialBnode deich:serial ?serial .
          ?serial deich:mainTitle ?seriesTitle . }

  UNION { <__PUBLICATIONURI__> deich:locationClassNumber ?locDewey }
  UNION { <__PUBLICATIONURI__> deich:hasFormatAdaptation ?formatAdaptation .
           ?formatAdaptation rdfs:label ?formatAtaptNoLang .
           BIND(str(?formatAtaptNoLang) AS ?formatAdaptNo)
           FILTER(lang(?formatAtaptNoLang) = "no") }
  UNION { <__PUBLICATIONURI__> deich:publishedBy ?publisher .
           ?publisher deich:name ?publishedBy . }

  # 2. Information from other publications:
  UNION {
    <__PUBLICATIONURI__> deich:publicationOf ?work  .
    ?anyP deich:publicationOf ?work ;
      deich:language ?anyLang ;
      deich:mainTitle ?anyMt .
    OPTIONAL { ?anyP deich:subtitle ?anySt }
    OPTIONAL { ?anyP deich:partTitle ?anyPt }
    OPTIONAL { ?anyP deich:partNumber ?anyPn }
    OPTIONAL { ?anyP deich:hasImage ?anyImage }
    BIND(str(?anyP) AS ?anyPub)
  }

  UNION {
        ?p4 deich:publicationOf ?work ;
           deich:hasMediaType ?mediaType .
        OPTIONAL {
          ?p4 deich:format ?mtFormatO .
          BIND(str(?mtFormatO) AS ?mtFormat)
        }
      }

  # 3. Information from work:
  UNION {  <__PUBLICATIONURI__> deich:publicationOf ?work .
           ?work a deich:Work ; deich:hasCompositionType ?compType .
           ?compType deich:prefLabel ?compTypeLabel . }
  UNION {  <__PUBLICATIONURI__> deich:publicationOf ?work .
           ?work a deich:Work ; deich:audience ?audienceO .
           BIND(str(?audienceO) AS ?audience) }
  UNION { <__PUBLICATIONURI__> deich:publicationOf ?work .
          ?work a deich:Work ; deich:publicationYear ?workYear .
          BIND(str(?workYear) AS ?workPublicationYear) }
  UNION { <__PUBLICATIONURI__> deich:publicationOf ?work .
          ?work a deich:Work ; deich:hasClassification ?classEntry .
          ?classEntry deich:hasClassificationNumber ?dewey . }
  UNION { <__PUBLICATIONURI__> deich:publicationOf ?work .
            ?work a deich:Work ; deich:literaryForm ?l .
            ?l rdfs:label ?litLabel .
            BIND(str(?litLabel) AS ?litform)
            FILTER(lang(?litLabel) = "no") }

  UNION {
    {
      <__PUBLICATIONURI__> deich:publicationOf ?work .
      ?work deich:subject ?subject .
      ?subject deich:prefLabel ?subjectName  .
      OPTIONAL { ?subject deich:specification ?subjectSpec }
    } UNION {
      <__PUBLICATIONURI__> deich:publicationOf ?work .
      ?work deich:subject ?subject .
      ?subject a deich:Work ;
              deich:mainTitle ?subjectName  .
    } UNION {
      <__PUBLICATIONURI__> deich:publicationOf ?work .
      ?work deich:subject ?subject .
      ?subject deich:name ?subjectName  .
      OPTIONAL { ?subject deich:specification ?subjectSpec }
    }
    BIND(IF(BOUND(?subjectSpec), CONCAT(?subjectName, " (", ?subjectSpec, ")"), ?subjectName) AS ?subjectLabel)
  }

  UNION {
      <__PUBLICATIONURI__> deich:publicationOf ?work .
      ?work deich:genre ?genre .
      ?genre a deich:Genre ;
            deich:prefLabel ?genreName  .
      OPTIONAL { ?work deich:genre ?genre. ?genre deich:specification ?genreSpec }
      BIND(IF(BOUND(?genreSpec), CONCAT(?genreName, " (", ?genreSpec, ")"), ?genreName) AS ?genreLabel)
    }

  # 4. Information from contributions on both publication and work:

    UNION {
            { <__PUBLICATIONURI__> deich:contributor ?contrib }
      UNION { <__PUBLICATIONURI__> deich:publicationOf ?work . ?work a deich:Work ; deich:contributor ?contrib }
        ?contrib a deich:Contribution ;
                 deich:agent ?contribAgent ;
                 deich:role ?contribRoleURI .
        ?contribAgent deich:name ?contribAgentName .
         BIND(str(?contribRoleURI) AS ?contribRole)
         OPTIONAL {
           ?contrib a deich:MainEntry .
           BIND(true as ?mainEntry) .
           ?contribAgent deich:name ?mainEntryName .
           OPTIONAL { ?contribAgent deich:nationality ?nationality . ?nationality rdfs:label ?nationLabelLang .
             BIND(str(?nationLabelLang) AS ?nationLabel)
             FILTER(lang(?nationLabelLang) = "no")
             }
           }
    }
    UNION {
        <__PUBLICATIONURI__> deich:hasPublicationPart ?pubPart .
        ?pubPart a deich:PublicationPart ;
                 deich:mainTitle ?pubPartMainTitle ;
                 OPTIONAL {
                    ?pubPart deich:agent ?pubPartAgent .
                    ?pubPartAgent deich:name ?pubPartAgentName
                 }
                 OPTIONAL {
                     ?pubPart deich:role ?pubPartRoleURI
                     BIND(str(?pubPartRoleURI) AS ?pubPartRole)
                 }
    }

  UNION { ?contribAgent deich:birthYear ?pBirthYearO . BIND(str(?pBirthYearO) AS ?contribAgentBirthYear) }
  UNION { ?contribAgent deich:deathYear ?pDeathYearO . BIND(str(?pDeathYearO) AS ?contribAgentDeathYear) }

}
# 1) Create media type for comic books

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX literaryForm: <http://data.deichman.no/literaryForm#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :format format:Book .
}

INSERT {
  ?pub :hasMediaType mediaType:ComicBook .
}

WHERE {
  ?pub :literaryForm literaryForm:comicBook ;
    a :Publication .
  OPTIONAL {?pub :format format:Book .}
}
;


# 2) Construct media type based on format, keep format

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>

WITH <http://deichman.no/migration>

INSERT {
  ?pub :hasMediaType ?mediaType
}

WHERE {
    ?pub a :Publication ;
         :format ?format  .

    VALUES (?format ?mediaType) {
      (format:DAISY                   mediaType:Audiobook)
      (format:ComputerFile            mediaType:E-book)
      (format:SignLanguageMovie       mediaType:Book)
      (format:Videotape               mediaType:Film)
      (format:DVD                     mediaType:Film)
      (format:Blu-ray                 mediaType:Film)
      (format:3DMovie                 mediaType:Film)
      (format:PersonalComputerGame    mediaType:Game)
      (format:Playstation2Game        mediaType:Game)
      (format:Playstation3Game        mediaType:Game)
      (format:Playstation4Game        mediaType:Game)
      (format:Xbox360Game             mediaType:Game)
      (format:XboxOneGame             mediaType:Game)
      (format:NintendoDSGame          mediaType:Game)
      (format:NintendoWiiGame         mediaType:Game)
      (format:Diapositive             mediaType:Other)
      (format:Map                     mediaType:Other)
      (format:CD-ROM                  mediaType:Other)
    }
}
;


# 3) Replace format with Media Type

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :format ?format .
}

INSERT {
  ?pub :hasMediaType ?mediaType
}

WHERE {
    ?pub a :Publication ;
         :format ?format  .

    VALUES (?format ?mediaType) {
      (format:Book                    mediaType:Book)
      (format:Audiobook               mediaType:Audiobook)
      (format:E-book                  mediaType:E-book)
      (format:LanguageCourse          mediaType:LanguageCourse)
      (format:MusicRecording          mediaType:MusicRecording)
      (format:SheetMusic              mediaType:SheetMusic)
      (format:Poster                  mediaType:Book)
      (format:Periodical              mediaType:Periodical)
    }
}
;


# 4) Add media type and format for card games and board games

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX format: <http://data.deichman.no/format#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :genre <http://data.deichman.no/genre/g2049303700> .
  ?pub :genre <http://data.deichman.no/genre/g2049303800> .
  ?pub :format format:PhysicalBody .
}

INSERT {
  ?pub :hasMediaType mediaType:Game ;
    :format ?format .
}

WHERE {
  ?pub :format format:PhysicalBody ;
    :genre ?genre ;
    a :Publication .

  VALUES (?genre  ?format) {
    (<http://data.deichman.no/genre/g2049303700>   format:BoardGame)
    (<http://data.deichman.no/genre/g2049303800>   format:CardGame)
  }
}
;


# 5) Add media type and format for musical instruments

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX tag: <http://data.deichman.no/tag/>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :format format:PhysicalBody .
}

INSERT {
  ?pub :hasMediaType mediaType:Other ;
    :format format:MusicalInstrument .
}

WHERE {
  ?pub :tags ?tag ;
    :format format:PhysicalBody ;
    a :Publication .
  FILTER (REGEX(?tag, "musikkinstrument", "i"))
}
;


#6) Prepare deleting physical objects

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX format: <http://data.deichman.no/format#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :format format:PhysicalBody .
}

INSERT {
  ?pub :format format:toBeDeleted .
}

WHERE {
  ?pub :format format:PhysicalBody ;
    a :Publication .
}
;


#6b) Prepare deleting periodical subscription records

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX migration: <http://migration.deichman.no/>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?pub :hasMediaType mediaType:Periodical .
}

INSERT {
  ?pub :format format:toBeDeleted .
}

WHERE {
  ?pub :hasMediaType mediaType:Periodical ;
    :mainTitle ?title ;
    a :Publication ;
    :recordId ?id .
  ?other :mainTitle ?title ;
    :hasMediaType mediaType:Periodical ;
    :publicationYear ?year .
  MINUS {?pub :publicationYear ?o .}
  MINUS {?child migration:parentRecordId ?id}
}
;


#7 ) Delete items with certain formats

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX format: <http://data.deichman.no/format#>

WITH <http://deichman.no/migration>

DELETE {
  ?pub ?p ?o .
}

WHERE {
    ?pub :format format:toBeDeleted ;
      a :Publication ;
      ?p ?o .
}
;


# 8) Remove Other where another media type is also present

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>

WITH <http://deichman.no/migration>

DELETE {
  ?pub :hasMediaType mediaType:Other
}

WHERE {
    ?pub :hasMediaType mediaType:Other ;
      :hasMediaType ?mediaType .
    FILTER (?mediaType != mediaType:Other)
}
;


# 9) Choose media type from publications with more than one

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>

WITH <http://deichman.no/migration>

DELETE {
  ?pub :hasMediaType ?delete .
}

WHERE {
    ?pub :hasMediaType ?delete ;
      :hasMediaType ?keep .

    VALUES (?delete ?keep) {
      (mediaType:Book               mediaType:LanguageCourse)
      (mediaType:MusicRecording     mediaType:Film)
      (mediaType:Book               mediaType:Audiobook)
      (mediaType:E-book             mediaType:Audiobook)
      (mediaType:Audiobook          mediaType:Film)
      (mediaType:Film               mediaType:Book)
      (mediaType:SheetMusic         mediaType:Film)
      (mediaType:Audiobook          mediaType:Game)
      (mediaType:MusicRecording     mediaType:Game)
      (mediaType:Audiobook          mediaType:LanguageCourse)
      (mediaType:MusicRecording     mediaType:Audiobook)
      (mediaType:MusicRecording     mediaType:Book)
      (mediaType:MusicRecording     mediaType:SheetMusic)
      (mediaType:SheetMusic         mediaType:Book)
      (mediaType:MusicRecording     mediaType:ComicBook)
    }
}
;


# 10) Construct work type based on media type

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX mediaType: <http://data.deichman.no/mediaType#>
PREFIX workType: <http://data.deichman.no/workType#>

WITH <http://deichman.no/migration>

INSERT {
  ?pub :hasWorkType ?workType
}

WHERE {
    ?pub a :Publication ;
         :hasMediaType ?mediaType  .

    VALUES (?mediaType ?workType) {
      (mediaType:Film                 workType:Film)
      (mediaType:Game                 workType:Game)
      (mediaType:Audiobook            workType:Literature)
      (mediaType:Book                 workType:Literature)
      (mediaType:E-book               workType:Literature)
      (mediaType:ComicBook            workType:Literature)
      (mediaType:Periodical           workType:Literature)
      (mediaType:MusicRecording       workType:Music)
      (mediaType:SheetMusic           workType:Music)
      (mediaType:LanguageCourse       workType:Other)
      (mediaType:Other                workType:Other)
    }
}
;


# 11) Replace format SignLanguageMovie with format adaptation

SPARQL
PREFIX : <http://data.deichman.no/ontology#>
PREFIX format: <http://data.deichman.no/format#>
PREFIX adaptation: <http://data.deichman.no/formatAdaptation#>

WITH <http://deichman.no/migration>

MODIFY

DELETE {
  ?publication :format format:SignLanguageMovie .
}

INSERT {
  ?publication :hasFormatAdaptation adaptation:signLanguage .
}

WHERE {
  ?publication :format format:SignLanguageMovie ;
    a :Publication .
}
;


# 12) Delete redundant formats
SPARQL
DEFINE sql:log-enable 2
PREFIX : <http://data.deichman.no/ontology#>
PREFIX format: <http://data.deichman.no/format#>

WITH <http://deichman.no/migration>

DELETE {
  ?publication :format ?o .
}

WHERE {
      ?publication :format ?o .
      VALUES ?o {
            format:CombiDocument
            format:Blu-rayROM
            format:WiiDisc
            format:NintendoGameCard
      }
}
;
